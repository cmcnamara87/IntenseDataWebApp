<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  horizontalAlign="center" gap="15"
		  buttonMode="true"
		  dragEnter="vgroup1_dragEnterHandler(event)"
		  dragDrop="vgroup1_dragDropHandler(event)"
		  dragExit="vgroup1_dragExitHandler(event)"
		  alpha="{selected ? 1 : 0.5}" xmlns:components="View.ERA.components.*"
		  creationComplete="vgroup1_creationCompleteHandler(event)"
		  removedFromStage="vgroup1_removedFromStageHandler(event)"
		  >

	<fx:Script>
		<![CDATA[
			import Controller.AppController;
			import Controller.ERA.CaseController;
			import Controller.IDEvent;
			import Controller.Utilities.AssetLookup;
			import Controller.Utilities.Auth;
			
			import Model.Model_ERARoom;
			
			import mx.controls.Alert;
			import mx.core.IUIComponent;
			import mx.core.UIComponent;
			import mx.events.DragEvent;
			import mx.events.FlexEvent;
			import mx.managers.DragManager;
			
			import spark.components.TileGroup;
			[Bindable]
			public var roomName:String = "";
			[Bindable]
			public var roomType:String = "";
			
			private var _roomData:Model_ERARoom;
			
			[Bindable]
			private var notificationCount:Number = 0;
			
			public function set roomData(roomData:Model_ERARoom):void {
				this._roomData = roomData;
				
				notificationCount = roomData.notificationCount;
			}
			

			[Bindable]
			public var unavailable:Boolean = false;
			
			[Bindable]
			public var icon:Class = AssetLookup.EvidenceManagerIcon;
			
			[Bindable]
			public var selected:Boolean = false;
			
			protected function vgroup1_dragDropHandler(event:DragEvent):void
			{
				var fileMoveEvent:IDEvent = new IDEvent(IDEvent.ERA_MOVE_FILE, true);
				fileMoveEvent.data.fileID = (event.dragInitiator as FileTile).fileID;
				fileMoveEvent.data.moveToRoomType = this.roomType;
				this.dispatchEvent(fileMoveEvent);
				
				(event.dragInitiator.parent as TileGroup).removeElement(event.dragInitiator as FileTile);
				
				this.alpha = 0.5;
			}


			protected function vgroup1_dragEnterHandler(event:DragEvent):void
			{
				// make it so that you cannot drag items into the evidence manager, or evidence box
				if(roomType == Model_ERARoom.EVIDENCE_MANAGEMENT || roomType == Model_ERARoom.EVIDENCE_ROOM || roomType == Model_ERARoom.POST_MORTEM) return;
				
				// make it so you cant drag into the one you are currently in (its selected)
				if(selected) return;
				
				// make it so that only the production manager or sys admin can move files between the forensic lab, exhibition and screenning room
				if(!CaseController.isProductionManager && !Auth.getInstance().isSysAdmin()) {
					if(roomType == Model_ERARoom.FORENSIC_LAB || roomType == Model_ERARoom.SCREENING_ROOM || roomType == Model_ERARoom.EXHIBIT) return;
				}
				
				// make it so only the sys admin can move files to the exhibition
				if(!Auth.getInstance().isSysAdmin()) {
					if(roomType == Model_ERARoom.EXHIBIT) return;
				}
				
				
				DragManager.acceptDragDrop(event.currentTarget as IUIComponent);
				DragManager.showFeedback( DragManager.MOVE );
				
				this.alpha = 1;
			}


			protected function vgroup1_dragExitHandler(event:DragEvent):void
			{
				this.alpha = 0.5
			}


			protected function vgroup1_creationCompleteHandler(event:FlexEvent):void
			{
				AppController.layout.addEventListener(IDEvent.ERA_NOTIFICATIONS_UPDATED, eraNotificationsUpdated);
			}
			
			private function eraNotificationsUpdated(e:Event):void {
				if(!_roomData) return; // we havnet set any room data info yet
				_roomData.updateNotificationCount();
				this.notificationCount = _roomData.notificationCount;
				trace("Updating Room", roomName, "notification count", this.notificationCount);
				notificationBadge.notificationCount = this.notificationCount;
			}

			protected function vgroup1_removedFromStageHandler(event:Event):void
			{
				AppController.layout.removeEventListener(IDEvent.ERA_NOTIFICATIONS_UPDATED, eraNotificationsUpdated);
				this.removeEventListener(Event.REMOVED_FROM_STAGE, vgroup1_removedFromStageHandler);
			}

		]]>
	</fx:Script>

	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<!--<s:Group width="70" height="70">
		
		<mx:Image source="{icon}" width="70" height="70" horizontalAlign="center" maintainAspectRatio="true"/>
		<s:Label x="18" text="x" fontSize="70" fontWeight="bold" color="0xFF0000" visible="{unavailable}" includeInLayout="{unavailable}"/>
	</s:Group>-->
	<s:Group width="70" height="70">		
		<s:BorderContainer backgroundColor="0x434343" borderVisible="false" height="70" width="70" visible="true" includeInLayout="true">
			<s:Label x="18" text="x" fontSize="70" fontWeight="bold" color="0xFF0000" visible="{unavailable}" includeInLayout="{unavailable}"/>
			<s:VGroup width="100%" height="100%" horizontalAlign="center">
					
			</s:VGroup>
			<s:filters>
				<s:DropShadowFilter distance="1" angle="90" color="0xFFFFFF" blurX="0" blurY="0"/>
			</s:filters>
		</s:BorderContainer>
		
		<components:NotificationBadge id="notificationBadge" top="-10" left="60" notificationCount="{notificationCount}" />
	</s:Group>
	
	<s:Label text="{roomName}" fontSize="18" color="0xcfcfcf" width="100%" textAlign="center">
		<s:filters>
			<s:DropShadowFilter distance="1" angle="270" color="0x000000" blurX="0" blurY="0"/>
		</s:filters>
	</s:Label>
</s:VGroup>
