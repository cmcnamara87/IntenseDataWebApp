<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="100%">

	<fx:Script>
		<![CDATA[
			import Controller.IDEvent;
			
			import Model.Model_ERAUser;
			
			import mx.collections.ArrayCollection;
			import mx.collections.ArrayList;
			
			import spark.events.IndexChangeEvent;
			
			[Bindable]
			public var role:String = "Role";
			
			private var allERAUsers:Array;
			private var userArray:Array;
			
			public function addAllUsers(allERAUsers:Array):void {
				this.allERAUsers = allERAUsers;
				var projectArray:ArrayCollection = new ArrayCollection();
				for each(var eraUser:Model_ERAUser in allERAUsers) {
					var object:Object = new Object();
					object.label = eraUser.lastName + ", " + eraUser.firstName + " (" + eraUser.username + ")";
					object.data = eraUser;
					projectArray.addItem(object);
				}
				allUsers.dataProvider = projectArray;
			}
			
			/**
			 * Makes sure we dont have an users added that no longer exist
			 */
			public function update():void {
				for(var i:Number = 0; i < userArray.length; i++) {
					var roleUser:Model_ERAUser = userArray[i] as Model_ERAUser;
					var found:Boolean = false;
					
					for each (var eraUser:Model_ERAUser in allERAUsers) {
						if(roleUser.username == eraUser.username) found = true;
					}
					if(found == false) {
						userArray.splice(i, 1);
						userlistDisplay();
					}
				}
			}
			
			public function addUserWithRole(user:Model_ERAUser):void {
				userArray.push(user);
				userlistDisplay();
			}
			/**
			 * Adds a list of users that have the set role
			 * @param userArray	The list of users (all model_erauser)
			 */
			public function addUsersWithRole(userArray:Array):void {
				// Save the user array (so we can update it later if need be)
				this.userArray = userArray;
				userlistDisplay();
			}
			
			private function userlistDisplay():void {
				currentUsers.removeAllElements();
				
				for each(var user:Model_ERAUser in userArray) {
					var userListItem:UserRoleItem = new UserRoleItem();
					userListItem.username = user.username;
					userListItem.firstName = user.firstName;
					userListItem.lastName = user.lastName;
					userListItem.roleComponent = this;
					userListItem.role = role;
					userListItem.deleteEventToThrow = IDEvent.ERA_REMOVE_USER_FROM_ROLE;
					currentUsers.addElement(userListItem);
				}
			}
			
			public function removeUser(username:String):void {
				for(var i:Number = 0; i < userArray.length; i++) {
					var roleUser:Model_ERAUser = userArray[i] as Model_ERAUser;
					if(roleUser.username == username) {
						userArray.splice(i, 1);
						addUsersWithRole(userArray);
						break;
					}
				}
			}
			
			protected function addUserButton_clickHandler(event:MouseEvent):void
			{
				// TODO Auto-generated method stub
				if(allUsers.selectedIndex == -1) return;
				
				addUserButton.setStyle("chromeColor", 0xCCCCCC);
				
				var addUserToRole:IDEvent = new IDEvent(IDEvent.ERA_ADD_USER_TO_ROLE, true);
				addUserToRole.data.eraUserData = allUsers.selectedItem.data;
				addUserToRole.data.role = role;
				addUserToRole.data.roleComponent = this;
				this.dispatchEvent(addUserToRole);	
				
				// Clear the dropdown
				allUsers.selectedIndex = -1;
			}

			protected function allUsers_changeHandler(event:IndexChangeEvent):void
			{
				// TODO Auto-generated method stub
				addUserButton.setStyle("chromeColor", 0x8FAFF7);
			}

		]]>
	</fx:Script>

	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<s:Label id="roleTitle" text="{Model_ERAUser.getRolePrettyName(role)}" fontSize="15" fontWeight="bold" color="0x5e6469">
	</s:Label>
	<s:Label text="{Model_ERAUser.getRoleDescription(role)}" color="0x5e6469" />
	<s:HGroup width="100%" paddingTop="5">
		<s:DropDownList id="allUsers" width="100%" change="allUsers_changeHandler(event)">
		</s:DropDownList>
		<s:Button label="Assign Role" id="addUserButton" click="addUserButton_clickHandler(event)">
		</s:Button>
	</s:HGroup>
	<s:VGroup id="currentUsers" width="100%" paddingTop="10" resizeEffect="Resize">
	</s:VGroup>
	<mx:HRule chromeColor="0x000000" width="100%" />
</s:VGroup>
