<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" width="100" gap="15" buttonMode="true" 
		  mouseOver="vgroup1_mouseOverHandler(event)" 
		  mouseOut="vgroup1_mouseOutHandler(event)" 
		  mouseDown="vgroup1_mouseDownHandler(event)" 
		  mouseUp="vgroup1_mouseUpHandler(event)"
		  mouseMove="vgroup1_mouseMoveHandler(event)" xmlns:components="View.ERA.components.*"
		  creationComplete="vgroup1_creationCompleteHandler(event)"
		  removedFromStage="vgroup1_removedFromStageHandler(event)">

	<fx:Script>
		<![CDATA[
			import Controller.AppController;
			import Controller.Dispatcher;
			import Controller.ERA.CaseController;
			import Controller.IDEvent;
			import Controller.Utilities.AssetLookup;
			import Controller.Utilities.Auth;
			
			import Model.Model_ERAFile;
			
			import mx.core.IUIComponent;
			import mx.events.FlexEvent;
			import mx.managers.DragManager;
			
			private var _fileData:Model_ERAFile;
			
			[Bindable] 
			public var title:String = "";
			
			[Bindable]
			private var description:String = "";
			
			[Bindable]
			public var fileID:Number;
			
			[Bindable]
			public var type:String;
			
			[Bindable]
			private var ext:String;
			
			[Bindable]
			private var version:Number = 0;
			
			[Bindable]
			private var screeningCount:Number = 0;
			
			[Bindable]
			private var exhibitionCount:Number = 0;
			
			[Bindable]
			private var rootMetaType:String;
			
			[Bindable]
			private var checkedOut:Boolean = false;
			
			[Bindable]
			private var notificationCount:Number = 0;
			
			public function set fileData(fileData:Model_ERAFile):void {
				this._fileData = fileData;
				
				updateGUI();
			}
			private function updateGUI():void {
				title =  _fileData.title;
				description = _fileData.description;
				fileID = _fileData.base_asset_id;
				type = _fileData.type;
				ext = _fileData.fileExt;
				version = _fileData.version;
				rootMetaType = _fileData.rootMetaType;
				checkedOut = _fileData.checkedOut;
				
				notificationCount = _fileData.notificationCount;
				screeningCount = _fileData.screeningCount;
				exhibitionCount = _fileData.exhibitionCount;
				
				notificationBadge.notificationCount = this.notificationCount;
				
				// If there are any researchers in the approval section
				// make the tick visible, and make it approved
				if(_fileData.researcherApproved.length || _fileData.researcherNotApproved.length || _fileData.monitorApproved.length || _fileData.monitorNotApproved.length) {
					// make it visible
					tickBadge.visible = true;
					tickBadge.includeInLayout = true;
					
					// Lets make the tooltip
					var approvedTooltip:String = "";
					// its going to show all the users who have approved/disappoved
					if(_fileData.researcherApproved.length) {
						approvedTooltip += "<b>Approved By Researcher(s)</b>:\n";
						for each(var researcherNameString:String in _fileData.researcherApproved) {
							approvedTooltip += researcherNameString + "\n";
						}
						approvedTooltip += "\n";
						tickBadge.show = true;
						tickBadge.approved = true;
					}
					
					
					if(_fileData.researcherNotApproved.length) {
						approvedTooltip += "Not Approved By Researcher(s):\n";
						for each(var researcherNameString:String in _fileData.researcherNotApproved) {
							approvedTooltip += researcherNameString + "\n";
						}
						approvedTooltip += "\n";
						tickBadge.show = true;
						tickBadge.approved = false;
					}
					
					
					// if the monitor approves or disapproves, it overwrites whatever the others have said basically (just for how it appears as either a x or a tick)
					if(_fileData.monitorNotApproved.length) {
						approvedTooltip += "Not Approved By Monitor(s):\n";
						for each(var monitorNameString:String in _fileData.monitorNotApproved) {
							approvedTooltip += monitorNameString + "\n";
						}
						approvedTooltip += "\n";
						tickBadge.show = true;
						tickBadge.approved = false;
					}
					
					
					if(_fileData.monitorApproved.length) {
						approvedTooltip += "Approved By Monitor(s):\n";
						for each(var monitorNameString:String in _fileData.monitorApproved) {
							approvedTooltip += monitorNameString + "\n";
						}
						approvedTooltip += "\n";
						
						tickBadge.show = true;
						tickBadge.approved = true;
					}
					
					// Now lets attach the tooltip
					tickBadge.toolTip = approvedTooltip;
					
				} else {
					tickBadge.show = false;
				}
			}
			
			[Bindable]
			public var dragEnabled:Boolean = true;
			
			private var mouseDown:Boolean = false;
		
			public function loadURL(id:Number):void {
//				if(rootMetaType == "image") {
//					icon.source = 'http://' + Recensio_Flex_Beta.serverAddress + ':' + Recensio_Flex_Beta.serverPort + '/mflux/icon.mfjp?_skey=' + Auth.getInstance().getSessionID() + '&id=' + fileID + '&version=0&size=100'
//				} else {
					icon.source = AssetLookup.getAssetImageClass(rootMetaType);
//				}
			}

			protected function vgroup1_mouseDownHandler(event:MouseEvent):void
			{
				mouseDown = true;
				
			}

			
			protected function vgroup1_mouseMoveHandler(event:MouseEvent):void
			{
				if(!dragEnabled) return;
				if(!mouseDown) return; // only do this if we are moving the mouse, after holding it down (aka dragging)
				var dragProxy:Image = new Image();
				dragProxy.height= 100;
				dragProxy.width= 100; 
				dragProxy.source = AssetLookup.getAssetImageClass(rootMetaType);
				
				DragManager.doDrag(event.currentTarget as IUIComponent, null, event, dragProxy);
				this.alpha = 0.5;
			}


			protected function vgroup1_mouseUpHandler(event:MouseEvent):void
			{
				this.alpha = 1;
				mouseDown = false;
				var showFileEvent:IDEvent = new IDEvent(IDEvent.ERA_SHOW_FILE, true);
				showFileEvent.data.fileID = fileID;
				trace("dispatching show file event");
				this.dispatchEvent(showFileEvent);
			}


			protected function vgroup1_mouseOverHandler(event:MouseEvent):void
			{
				this.alpha = 0.8;
				// TODO Auto-generated method stub
			}


			protected function vgroup1_mouseOutHandler(event:MouseEvent):void
			{
				this.alpha = 1;
				// TODO Auto-generated method stub
			}

			protected function vgroup1_creationCompleteHandler(event:FlexEvent):void
			{
				AppController.layout.addEventListener(IDEvent.ERA_NOTIFICATIONS_UPDATED, eraNotificationsUpdated);
			}

			private function eraNotificationsUpdated(e:Event):void {
				if(!_fileData) return; // we havnet set any room data info yet
				_fileData.updateNotificationCount();
				this.updateGUI();
			}
			

			protected function vgroup1_removedFromStageHandler(event:Event):void
			{
				AppController.layout.removeEventListener(IDEvent.ERA_NOTIFICATIONS_UPDATED, eraNotificationsUpdated);
				this.removeEventListener(Event.REMOVED_FROM_STAGE, vgroup1_removedFromStageHandler);
			}

		]]>
	</fx:Script>

	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<s:Group width="100" height="100">
		<mx:Image id="icon" height="100" width="100" verticalAlign="middle" maintainAspectRatio="true" toolTip="Description: {description}" alpha="{checkedOut ? 0.5 : 1}" />
		<components:NotificationBadge id="notificationBadge" left="75" notificationCount="{notificationCount}" />		
		<components:TickBadge horizontalCenter="0" id="tickBadge" show="false" />
	</s:Group>
	
	<s:Label text="{title}{version > 0 ? '_v' + version : ''}{screeningCount > 0 ? '_r' + screeningCount : ''}{exhibitionCount > 0 ? '_e' + exhibitionCount : ''} ({ext})" width="100%" textAlign="center" />
</s:VGroup>
