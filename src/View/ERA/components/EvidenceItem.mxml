<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:components="View.components.*" xmlns:components1="View.ERA.components.*">

	<fx:Script>
		<![CDATA[
			import Controller.AppController;
			import Controller.IDEvent;
			import Controller.Utilities.AssetLookup;
			
			import Model.AppModel;
			import Model.Model_Base;
			import Model.Model_ERAFile;
			import Model.Model_ERALogItem;
			import Model.Model_ERARoom;
			
			import flashx.textLayout.tlf_internal;
			
			import mx.controls.Alert;
			private var fileReference:FileReference;
			
			private var logItem:Model_ERALogItem;
			public var allLogItemsArray2:Array = new Array();
			
			/*================================ EVENT LISTENERS ============================== */
			
			public function getID():Number {
				if(logItem == null) return 0;
				return logItem.base_asset_id;
			}
			/**
			 * The Upload button was clicked.
			 */
			protected function uploadButton_clickHandler(event:MouseEvent):void
			{
				if(logItem.uploaded) {
					var showFileEvent:IDEvent = new IDEvent(IDEvent.ERA_SHOW_FILE, true);
					showFileEvent.data.fileID = logItem.dataItemID;
					showFileEvent.data.roomType = Model_ERARoom.EVIDENCE_ROOM;
					dispatchEvent(showFileEvent);
					return;
				}
				
				fileReference = new FileReference();
//				fileReference.browse(AssetLookup.getFileFilter(logItem.type));
				fileReference.browse(AssetLookup.getFileTypes());
				fileReference.addEventListener(Event.SELECT, onFileSelect);
				fileReference.addEventListener(Event.CANCEL, fileSelectCancelled);

				uploadButton.label = "Uploading...";
				uploadButton.enabled = false;
			}
			
			private function fileSelectCancelled(e:Event):void {
				uploadButton.label = "Upload";
				uploadButton.enabled = true;
			}
			/**
			 * A file for upload was selected
			 */
			private function onFileSelect(e:Event):void {
				uploadProgress.includeInLayout = true;
				uploadProgress.visible = true;
				
				var uploadEvent:IDEvent = new IDEvent(IDEvent.ERA_SAVE_FILE, true);
				uploadEvent.data.type = evidenceType.selectedItem;
				uploadEvent.data.title = title.text;
				uploadEvent.data.description = description.text;
				uploadEvent.data.evidenceItem = this;
				uploadEvent.data.fileReference = fileReference;
				uploadEvent.data.logItemID = logItem.base_asset_id;
				
				uploadButton.enabled = false;
				
				dispatchEvent(uploadEvent);
//				AppModel.getInstance().startFileUpload(dataObject);
			}
			
			public function showComplete(evidence:Model_ERAFile):void {
				uploadButton.label = "View";
				uploadButton.enabled = true;
				uploadProgress.includeInLayout = false;
				uploadProgress.visible = false;
				logItem.uploaded = true;
				logItem.dataItemID = evidence.base_asset_id;
			}
			public function showProgress(percentage:Number):void {
				progressBar.showPercentage(percentage);
			}
			private function ioErrorHandler(e:IOError):void {
				
			}
			
			
			/**
			 * The cancel upload button was clicked
			 */
			protected function cancelUploadButton_clickHandler(event:MouseEvent):void
			{
				fileReference.cancel();
				
				uploadProgress.includeInLayout = false;
				uploadProgress.visible = false;
				
				uploadButton.label = "Upload";
				uploadButton.enabled = true;
				
				
			}

			/**
			 * The useful checkbox was ticked
			 */
			protected function usefulCheckbox_changeHandler(event:Event):void
			{
				if((event.target as CheckBox).selected == true) {
					// The useful box was checked
					processedCheckbox.enabled = true;
					uploadButton.enabled = false;
				} else {
					// The useful box has been unchecked
					processedCheckbox.enabled = false;
					uploadButton.enabled = false;
				}
				
				var usefulEvent:IDEvent = new IDEvent(IDEvent.ERA_UPDATE_LOG, true);
				usefulEvent.data.logItemID = logItem.base_asset_id;
				usefulEvent.data.elementName = "useful";
				usefulEvent.data.value = (event.target as CheckBox).selected;
				usefulEvent.data.evidenceItem = this;
				dispatchEvent(usefulEvent);
			}
			
			protected function processedCheckbox_clickHandler(event:MouseEvent):void
			{
				// TODO Auto-generated method stub
				if((event.target as CheckBox).selected == true) {
					// The useful box was checked
					uploadButton.enabled = true;
				} else {
					// The useful box has been unchecked
					uploadButton.enabled = false;
				}
				
				var usefulEvent:IDEvent = new IDEvent(IDEvent.ERA_UPDATE_LOG, true);
				usefulEvent.data.logItemID = logItem.base_asset_id;
				usefulEvent.data.elementName = "processed";
				usefulEvent.data.value = (event.target as CheckBox).selected;
				usefulEvent.data.evidenceItem = this;
				dispatchEvent(usefulEvent);
				
			}


			/**
			 * The save button was clicked
			 */
			protected function saveButton_clickHandler(event:MouseEvent):void
			{
				// Lets check if there is a file with this name already
				trace("save button clicked, ", allLogItemsArray2.length);
				for each(var logItem:Model_ERALogItem in allLogItemsArray2) {
					trace("checking for match", title.text, ":", logItem.title);
					if(logItem.title == title.text) {
//						AppController.layout.notificationBar.showError("A piece of evidence this name already exists. Please choose a different name.");
						return;
					}
				}
				
				if(evidenceType.selectedIndex == -1) {
//					AppController.layout.notificationBar.showError("Please specify a type");
					return;
				}
				
				if(title.text == "" || title.text == "name") {
//					AppController.layout.notificationBar.showError("Please enter a name for the Evidence.");
					return;
				}
				
				if(description.text == "" || description.text == "description") {
//					AppController.layout.notificationBar.showError("Please enter a description for the Evidence.");
					return;
				}
				
				
				// Disable the save button while we save
				saveButton.label = "Saving...";
				saveButton.enabled = false;
				
				// Pass the type, title and description to the controller and this item itself
				var evidenceSavedEvent:IDEvent = new IDEvent(IDEvent.ERA_SAVE_LOG_ITEM, true);
				evidenceSavedEvent.data.type = evidenceType.selectedItem;
				evidenceSavedEvent.data.title = title.text;
				evidenceSavedEvent.data.description = description.text;
				evidenceSavedEvent.data.evidenceItem = this;
				dispatchEvent(evidenceSavedEvent);
			}
			
			public function showCheckBoxes():void {
				saveButton.visible = false;
				saveButton.includeInLayout = false;
				actionGroup.visible = true;
				actionGroup.includeInLayout = true;
			}

			public function addLogItemData(logItem:Model_ERALogItem):void {
				
				// Look to see if the data is already in our array of stored log items
				// if it is, pull it out
				for(var i:Number = 0; i < allLogItemsArray2.length; i++) {
					var searchedLogItems:Model_ERALogItem = allLogItemsArray2[i] as Model_ERALogItem;
					if(logItem.base_asset_id == searchedLogItems.base_asset_id) {
						allLogItemsArray2.splice(i, 1);
					}
				}
				
				// Add the item (only going to be one copy now, because of the lines above);
				allLogItemsArray2.push(logItem);
//				
				
				this.logItem = logItem;
				
				showCheckBoxes();
				
				evidenceType.selectedItem = logItem.type;
				title.text = logItem.title;
				title.setStyle('color', '0x000000');
				description.text = logItem.description;
				description.setStyle('color', '0x000000');
				
				// Disable the text entry boxes, because theyh ave already been saved
				evidenceType.enabled = false;
				title.enabled = false;
				description.enabled = false;
				
				usefulCheckbox.selected = logItem.useful;
				if(usefulCheckbox.selected == true) {
					processedCheckbox.enabled = true;
				}
				
				processedCheckbox.selected = logItem.processed;
				if(processedCheckbox.selected == true) {
					uploadButton.enabled = true;
				}
				
				if(logItem.type == "Physical Object") {
					// its a physical object, so you cant upload it
					uploadButton.label = "Disabled";
					uploadButton.enabled = false;
				} else {
					
					if(logItem.uploaded) {
						uploadButton.label = "View";
					} else {
						uploadButton.label = "Upload";
					}
					
				}
				
				
				
				returnedCheckbox.selected = logItem.returned;
				
				if(logItem.returned) {
					collectedCheckbox.enabled = true;
				}
				collectedCheckbox.selected = logItem.collected;
			}

			protected function deleteEvidence_clickHandler(event:MouseEvent):void
			{
				if(logItem == null) {
//					trace(this.owner, "owner for evidence item");
//					trace(this.parent, "parent for evidence item");
					(this.parent as VGroup).removeElement(this);
					return
				}
				var deleteEvent:IDEvent = new IDEvent(IDEvent.ERA_DELETE_LOG_ITEM, true);
				deleteEvent.data.logItem = logItem;
				dispatchEvent(deleteEvent);
			}

			protected function description_focusInHandler(event:FocusEvent):void
			{
				// TODO Auto-generated method stub
				if(description.text == "description") {
					description.text = "";
					description.setStyle('color', '0x000000');
				}
			}


			protected function title_focusInHandler(event:FocusEvent):void
			{
				// TODO Auto-generated method stub
				if(title.text == "name") {
					title.text = "";
					title.setStyle('color', '0x000000');
				}
			}


			protected function description_focusOutHandler(event:FocusEvent):void
			{
				// TODO Auto-generated method stub
				if(description.text == "") {
					description.text = "description";
					description.setStyle('color', '0x888888');
				}
			}


			protected function title_focusOutHandler(event:FocusEvent):void
			{
				// TODO Auto-generated method stub
				if(title.text == "") {
					title.text = "name";
					title.setStyle('color', '0x888888');
				}
			}

			protected function collectedCheckbox_clickHandler(event:MouseEvent):void
			{
				checkCollectedBox(collectedCheckbox.selected);
			}

			public function checkCollectedBox(value:Boolean):void {
				// TODO Auto-generated method stub
				
				var usefulEvent:IDEvent = new IDEvent(IDEvent.ERA_UPDATE_LOG, true);
				usefulEvent.data.logItemID = logItem.base_asset_id;
				usefulEvent.data.elementName = "collected";
				usefulEvent.data.value = value;
				usefulEvent.data.evidenceItem = this;
				dispatchEvent(usefulEvent);
			}

			protected function returnedCheckbox_changeHandler(event:Event):void
			{
				checkForCollectionBox(returnedCheckbox.selected);
			}
			
			public function checkForCollectionBox(value:Boolean):void {
				collectedCheckbox.enabled = value;
				if(collectedCheckbox.selected && value == false) {
					// we need to uncheck it
					checkCollectedBox(false);
				}
				
				var usefulEvent:IDEvent = new IDEvent(IDEvent.ERA_UPDATE_LOG, true);
				usefulEvent.data.logItemID = logItem.base_asset_id;
				usefulEvent.data.elementName = "returned";
				usefulEvent.data.value = value;
				usefulEvent.data.evidenceItem = this;
				dispatchEvent(usefulEvent);
			}

		]]>
	</fx:Script>

	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<s:HGroup verticalAlign="middle">
		<s:Button id="deleteEvidence" label="X" width="30" click="deleteEvidence_clickHandler(event)"/>
		<s:DropDownList id="evidenceType" width="200">
			<s:ArrayList>
				<fx:String>Audio</fx:String>
				<fx:String>Image</fx:String>
				<fx:String>PDF</fx:String>
				<fx:String>Office Documents</fx:String>
				<fx:String>Physical Object</fx:String>
				<fx:String>Research Description</fx:String>
				<fx:String>Research Statement</fx:String>
				<fx:String>Video</fx:String>
			</s:ArrayList>
		</s:DropDownList>
		<s:TextInput id="title" text="name" color="0x888888" focusIn="title_focusInHandler(event)" focusOut="title_focusOutHandler(event)"/>
		<s:TextInput id="description" width="300" text="description" color="0x888888" focusIn="description_focusInHandler(event)" focusOut="description_focusOutHandler(event)"/>
		<s:Button label="Save" id="saveButton" click="saveButton_clickHandler(event)" />
		<s:HGroup id="actionGroup" visible="false" includeInLayout="false">
			<s:CheckBox label="Accepted" id="usefulCheckbox" change="usefulCheckbox_changeHandler(event)"/>
			<s:CheckBox label="Processed" id="processedCheckbox" enabled="false" click="processedCheckbox_clickHandler(event)"/>
			<s:Button label="Upload" id="uploadButton" click="uploadButton_clickHandler(event)" enabled="false" />
			<s:CheckBox id="returnedCheckbox" label="For Collection" change="returnedCheckbox_changeHandler(event)" />
			<s:CheckBox id="collectedCheckbox" label="Collected" click="collectedCheckbox_clickHandler(event)" enabled="false"/>
		</s:HGroup>
	</s:HGroup>
	<s:HGroup id="uploadProgress" width="100%" visible="false" includeInLayout="false">
		<components1:LoadingBar id="progressBar" width="100%" />
		<s:Button width="70" label="Cancel" id="cancelUploadButton" click="cancelUploadButton_clickHandler(event)"/>
	</s:HGroup>
</s:VGroup>
